IDEAL
MODEL small
STACK 100h

DATASEG

; *********************************OFFSET CONSTANTS*********************************

PhysicsComponentId            equ 4
PhysicsComponentAxisX         equ 0
PhysicsComponentVelocityX     equ 0
PhysicsComponentAccelerationX equ 2
PhysicsComponentFrictionX     equ 4
PhysicsComponentMaxVelocityX  equ 6
PhysicsComponentAxisY         equ 8
PhysicsComponentVelocityY     equ 8
PhysicsComponentAccelerationY equ 10
PhysicsComponentFrictionY     equ 12
PhysicsComponentMaxVelocityY  equ 14

; ********************************************

PhysicsComponentAxisVelocity     equ 0
PhysicsComponentAxisAcceleration equ 2
PhysicsComponentAxisFriction     equ 4
PhysicsComponentAxisMaxVelocity  equ 6

; ********************************************

AnimatorComponentId                    equ 3
AnimatorComponentCurrentAnimationIndex equ 0
AnimatorComponentCurrentFrameIndex     equ 1
AnimatorComponentAnimationAmount       equ 2
AnimatorComponentData                  equ 3

AnimationId         equ 2
AnimationSize       equ 1
AnimationSpeed      equ 2
AnimationData       equ 3

; ********************************************

RenderComponentId                  equ 1
RenderComponentPosition            equ 0
RenderComponentImage               equ 2
RenderComponentPreviousPosition    equ 4
RenderComponentWidth               equ 6
RenderComponentHeight              equ 8
RenderComponentBackground          equ 10

; ********************************************

MouseComponentId equ 7
MouseComponentLeftClick  equ 0
MouseComponentRightClick equ 1

; ********************************************

ControllerMaxComponentAmount equ 0
ControllerComponentData      equ 1

ComponentId       equ 0
ComponentLocation equ 1

ImageWidth  equ 0
ImageHeight equ 2
ImageData   equ 4

PrintManagerId               equ 5
PrintManagerControllerAmount equ 1
PrintManagerData             equ 2

SegmentWrapperId       equ 6
SegmentWrapperSegment  equ 1
SegmentWrapperOriginal equ 3

; *********************************CONTROLLERS START********************************


; *********************************ANIMATIONS START********************************


; *********************************OBJECTS START********************************

KeyStateTable db 256 dup(0) ; Table to store key states (0 = released, 1 = pressed)

DirectionMultiplier equ 16*3
JumpHeight equ 16*4

SubPixelAmount equ 16

; *********************************GENERAL START********************************

animationTime dw 0
gameLoopSpeed equ 17 ; in ms (to calculate FPS -> 1024 / FPS)
globalTime dw 0      ; in ms

; Declare variables to store the old ISR
oldKeyboardHandlerOffset dw ?
oldKeyboardHandlerSegment dw ?

; Save the original mask for restoration
previousIRQMask db ?

printManager db PrintManagerId, 0
dw 50 dup(0)

; ************************** ;

CODESEG

; input - Mouse pointer
proc initMouse
    push bp
    mov bp, sp
    push ax
    mov ax, 0 ; init Mouse
    int 33h
    push [bp + 4] ; mouse pointer
    push (100 * 320) + 160
    call initController
    pop ax
    pop bp
    ret 2
endp initMouse

proc updateMouse
    push bp
    mov bp, sp
    push bx
    push ax
    push si
    push dx
    push cx
    push di

    mov si, [bp + 6] ; Mouse pointer

    mov ax, 3 ; get mouse position cx - x, dx - y
    int 33h

    shr cx, 1 ; 0-639 -> 0-319
    cmp cx, 319 
    jne .skipFixX
    dec cx
.skipFixX:

    cmp dx, 199 
    jne .skipFixY
    dec dx
.skipFixY:

    mov ax, dx ;  compute screen position
    xor dx, dx
    mov di, 320
    mul di
    add ax, cx
    
    push si ; mouse controller
    push ax ; position
    ; call moveControllerByPosition

    push [bp + 4]
    push si
    call uploadController

    pop di
    pop cx
    pop dx
    pop si
    pop ax
    pop bx
    pop bp
    ret 4
endp updateMouse

proc keyboardHandler far              
	push ax
	push bx
    push cx
    push dx
	push di
	push si
	; read keyboard scan code
    in al, 60h

	; update keyboard state
    xor bh, bh
    mov bl, al
    and bl, 7Fh     ; bl = scan code
	
	; save to the keyboard array
    push cx
	mov cx, 7
    shr al, cl ; al = 0 if pressed, 1 if released
	pop cx
    xor al, 1  ; al = 1 if pressed, 0 if released
    mov [cs:KeyStateTable + bx], al  ; save pressed buttons in array
	
	; send EOI to XT keyboard
    in  al, 61h
    mov ah, al
    or  al, 80h
    out 61h, al
    mov al, ah
    out 61h, al

	; send EOI to master PIC
    mov al, 20h
    out 20h, al
	
    pop si
    pop di
    pop dx
    pop cx
    pop bx
    pop ax
    iret
endp keyboardHandler

; interrupt that hooks on to int70h
; that is called by the computer
; and call the "periodic" method 1024 times a sec
proc clock far
    push bp
    mov bp, sp
    push ax
    push bx
    push dx
    push ds
    ; Preserve port 70h contents
    in al, 70h
    mov bx, ax
    ; Read status register C to acknowledge RTC interrupt
    mov al, 0Ch
    out 70h, al
    mov al, 8Ch
    out 70h, al
    in al, 71h
    ; Restore port 70h contents
    mov ax, bx
    out 70h, al
    ; Notify PICs that interrupt is handled
    mov al, 20h
    out 0A0h, al
    out 020h, al
    push offset globalTime
    call periodic ; every tick (1024 times a sec)
    pop ds
    pop dx
    pop bx
    pop ax
    pop bp
    iret
endp clock

; setup the game clock
proc setUpClock
    push bp
    mov bp, sp
    push ax
    push ds
    push dx

    cli
    ; Save original mask of secondary PIC
    in al, 0A1h
    mov [bp + 4], al
    and al, 0FEh ; Enable IRQ 8 (RTC)
    out 0A1h, al

    ; Set frequency divisor in RTC register A
    mov al, 0Ah
    out 70h, al
    mov al, 8Ah
    out 70h, al
    in al, 71h
    and al, 11110000b ; Clear rate bits
    or al, 0110b      ; Set rate to 1024 Hz
    out 71h, al
    in al, 71h        ; Ensure the write is complete

    ; Enable periodic interrupts in RTC register B
    mov al, 0Bh
    out 70h, al
    mov al, 8Bh
    out 70h, al
    in al, 71h
    or al, 40h        ; Set PI (bit 6)
    out 71h, al
    in al, 71h        ; Acknowledge the change

    ; Set new interrupt handler for interrupt 70h
    mov al, 70h       ; Interrupt vector 70h
    mov ah, 25h       ; Set interrupt vector
    mov dx, offset clock ; Offset of the ISR
    push seg clock
    pop ds
    int 21h
    sti

    pop dx
    pop ds
    pop ax
    pop bp
    ret 2
endp setUpClock

; clean up the game's clock
proc cleanUpClock
    push bp
    mov bp, sp
    push ax
    ; Restore original mask of secondary PIC
    mov al, [bp + 4] ; mask pointer
    out 0A1h, al
    pop ax
    pop bp
    ret 2
endp cleanUpClock

; ***********************GENERAL GETTERS*********************

proc getControllerWordValue
    push bp
    mov bp, sp
    push bx
    mov bx, [bp + 8] ; controller pointer
    push bx
    push [bp + 6] ; componentId
    call getControllerComponent
    pop bx
    add bx, [bp + 4] ; parameter
    mov bx, [bx]
    mov [bp + 8], bx
    pop bx
    pop bp
    ret 4
endp getControllerWordValue

proc getControllerWordOffset
    push bp
    mov bp, sp
    push bx
    mov bx, [bp + 8] ; controller pointer
    push bx
    push [bp + 6] ; componentId
    call getControllerComponent
    pop bx
    add bx, [bp + 4] ; parameter
    mov [bp + 8], bx
    pop bx
    pop bp
    ret 4
endp getControllerWordOffset

proc getControllerByteValue
    push bp
    mov bp, sp
    push bx
    mov bx, [bp + 8] ; controller pointer
    push bx
    push [bp + 6] ; componentId
    call getControllerComponent
    pop bx
    add bx, [bp + 4] ; parameter
    mov bl, [bx]
    mov [bp + 8], bl
    pop bx
    pop bp
    ret 4
endp getControllerByteValue

; ***********************GENERAL SETTERS*********************

proc setControllerWordValue
    push bp
    mov bp, sp
    push ax
    push bx
    mov bx, [bp + 10] ; controller pointer
    push bx
    push [bp + 6] ; componentId
    call getControllerComponent
    pop bx
    add bx, [bp + 4] ; parameter
    mov ax, [bp + 8] ; value
    mov [bx], ax
    pop bx
    pop ax
    pop bp
    ret 8
endp setControllerWordValue

proc addControllerWordValue
    push bp
    mov bp, sp
    push ax
    push bx
    mov bx, [bp + 10] ; controller pointer
    push bx
    push [bp + 6] ; componentId
    call getControllerComponent
    pop bx
    add bx, [bp + 4] ; parameter
    mov ax, [bp + 8] ; addition
    add [bx], ax
    pop bx
    pop ax
    pop bp
    ret 8
endp addControllerWordValue

proc setControllerByteValue
    push bp
    mov bp, sp
    push ax
    push bx
    mov bx, [bp + 10] ; controller pointer
    push bx
    push [bp + 6] ; componentId
    call getControllerComponent
    pop bx
    add bx, [bp + 4] ; parameter
    mov al, [bp + 8] ; value
    mov [bx], al
    pop bx
    pop ax
    pop bp
    ret 8
endp setControllerByteValue

proc addControllerByteValue
    push bp
    mov bp, sp
    push ax
    push bx
    mov bx, [bp + 10] ; controller pointer
    push bx
    push [bp + 6] ; componentId
    call getControllerComponent
    pop bx
    add bx, [bp + 4] ; parameter
    mov al, [bp + 8] ; addition
    add [bx], al
    pop bx
    pop ax
    pop bp
    ret 8
endp addControllerByteValue

; ****************************************

; input - word directionX pointer, word directionY pointer
proc updateDirection
    push bp
    mov bp, sp
    push ax
    push si
    push di
    mov si, [bp + 6]  ; directionX pointer
    mov di, [bp + 4]  ; directionY pointer
    mov [word ptr si], 0
    mov [word ptr di], 0
    cmp [byte ptr KeyStateTable + 1Eh], 1 ; Check if 'A' key is pressed (scan code 1Eh)
    je .setLeft
    cmp [byte ptr KeyStateTable + 20h], 1  ; Check if 'D' key is pressed (scan code 20h)
    je .setRight
    jmp .checkY
.setLeft:
    mov [word ptr si], -1 * DirectionMultiplier
    jmp .checkY
.setRight:
    mov [word ptr si], DirectionMultiplier
.checkY:
    cmp [byte ptr KeyStateTable + 11h], 1 ; Check if 'W' key is pressed (scan code 11h)
    je .setUp
    cmp [byte ptr KeyStateTable + 1Fh], 1 ; Check if 'S' key is pressed (scan code 1Fh)
    je .setDown
    jmp .end
.setUp:
    mov [word ptr di], -1 * DirectionMultiplier
    jmp .end
.setDown:
    mov [word ptr di], DirectionMultiplier
.end:
    pop di
    pop si
    pop ax
    pop bp
    ret 4
endp updateDirection

; clears the screen (turns all pixels to 0)
proc clearScreen
    push di
    mov di, 64001
.clearLoop:
    dec di
    mov [byte ptr es:di], 0
    jnz .clearLoop
    pop di
    ret
endp clearScreen

; input - palette pointer
; sets it to the screen's palette
proc setPalette
    push bp
    mov bp, sp
    push ax
    push si
    push cx
    push dx
	mov si, [bp + 4]
	mov cx, 256
	mov dx, 3C8h
	mov al, 0
	out dx, al ; copy starting color to port 3C8h
	inc dx     ; copy palette itself to port 3C9h
.setLoop:
	mov al, [si]   ; red
	out dx, al
	mov al, [si + 1] ; green
	out dx, al
	mov al, [si + 2] ; blue
	out dx, al
	add si, 3      ; move to next color
	loop .setLoop
    pop dx
    pop cx
    pop si
    pop ax
    pop bp
	ret 2
endp setPalette

; input - current pixel pos
; output - (ah = 0/1 x border) (al = 0/1 y border)
proc checkImageBorders
    push bp
    mov bp, sp
    push ax
    push cx
    push dx
    push bx

    xor bx, bx
    mov ax, [bp + 4]  ; current pos

    mov cx, 320
    xor dx, dx
    idiv cx ; ax - y, dx - x
    cmp dx, 319
    je .setTrueX
    cmp ax, 199
    je .setTrueY

    jmp .end
.setTrueX:
    mov bh, 1
    jmp .end
.setTrueY:
    mov bl, 1
.end:
    mov [bp + 4], bx
    pop bx
    pop dx
    pop cx
    pop ax
    pop bp
    ret
endp checkImageBorders

proc printImageBySize
    push bp
    mov bp, sp
    push di
    push ax
    push bx
    push cx
    push dx
    push si
    mov bx, [bp + 10] ; image pointer
    mov cx, [bp + 8]  ; height
    mov dx, [bp + 6]  ; width
    mov di, [bp + 4]  ; screen position (left top)
    mov si, 0 ; reset width counter
.printLoop:
    push di
    call checkImageBorders
    pop ax
    cmp al, 1 ; if hit bottom, exit
    je .end
    cmp ah, 1 ; if hit sides, move to next row
    jne .skipCheckX
.disableRow:
    ; set the counters to the next row
    sub di, si
    sub bx, si
    add di, dx
    add bx, dx
    jmp .nextPixelRow
.skipCheckX:
    mov al, [byte ptr bx] ; get the color
    cmp al, -1
    je .skipPixel ; skip if it's empty (-1)
    mov [byte ptr es:di], al
.skipPixel:
    inc di ; move to the next pixel from screen
    inc bx ; move to the next pixel from image
    inc si ; inc the current width counter
    cmp si, dx ; if reached end of the row
    jne .printLoop
.nextPixelRow:
    add di, 320
    sub di, dx ; point to the next column
    xor si, si ; reset width counter
    dec cx
    jnz .printLoop
.end:
    pop si
    pop dx
    pop cx
    pop bx
    pop ax
    pop di
    pop bp
    ret 8
endp printImageBySize

; input - image pointer, position (left top corner)
; prints the image at the specified location
proc printImage
    push bp
    mov bp, sp
    push ax
    push bx
    mov bx, [bp + 6] ; image pointer
    mov ax, bx
    add ax, ImageData
    push ax ; image data (colors)
    push [bx + ImageHeight]
    push [bx + ImageWidth]
    push [bp + 4] ; screen position (left top)
    call printImageBySize
    pop bx
    pop ax
    pop bp
    ret 4
endp printImage

; input - controller pointer
; prints it to the screen
proc printController
    push bp
    mov bp, sp
    push di
    mov di, [bp + 4] ; controller pointer
    push di
    push RenderComponentId
    call getControllerComponent
    pop di ; component
    push [word ptr di + RenderComponentImage]
    push [word ptr di + RenderComponentPosition]
    call printImage
    pop di
    pop bp
    ret 2
endp printController

; saves the background to the controller so it could move
; without affecting the background
proc saveController
    push bp
    mov bp, sp
    push di
    push bx
    push cx
    push dx
    push si
    mov di, [bp + 4]  ; controller
    push di
    push RenderComponentId
    call getControllerComponent
    pop di ; component

    mov bx, di
    add bx, RenderComponentBackground

    mov cx, [di + RenderComponentHeight]
    mov dx, [di + RenderComponentWidth]
    mov di, [di + RenderComponentPosition]
    mov si, 0  ; width counter
.saveLoop:
    mov al, [es:di]
    mov [bx], al
    inc di
    inc bx
    inc si
    cmp si, dx ; if reached end of the row
    jne .saveLoop
    add di, 320
    sub di, dx ; point to the next column
    xor si, si
    dec cx
    jnz .saveLoop
    pop si
    pop dx
    pop cx
    pop bx
    pop di
    pop bp
    ret 2
endp saveController

proc printBackground
    push bp
    mov bp, sp
    push di
    push dx
    mov di, [bp + 4] ; controller pointer

    push di
    push RenderComponentId
    call getControllerComponent
    pop di ; component

    mov dx, di
    add dx, RenderComponentWidth 
    push dx ; (width, height, color bytes)
    push [word ptr di + RenderComponentPreviousPosition]
    call printImage

    mov dx, [di + RenderComponentPosition]
    mov [di + RenderComponentPreviousPosition], dx
    
    pop dx
    pop di
    pop bp
    ret 2
endp printBackground

; input - controller pointer, word init position
; init the controller to the set position (saves the background, prints and moves to the location)
proc initController
    push bp
    mov bp, sp
    push di
    push ax
    push bx
    mov di, [bp + 6] ; controller pointer
    mov ax, [bp + 4] ; set position

    push di
    push RenderComponentId
    call getControllerComponent
    pop bx ; component

    mov [bx + RenderComponentPosition], ax
    mov [bx + RenderComponentPreviousPosition], ax
    
    push di
    call saveController
    push di
    call printController
    pop bx
    pop ax
    pop di
    pop bp
    ret 4
endp initController

; input - animated controller pointer
; moves the current animation a frame (if last frame resets it)
proc moveToNextAnimationFrame
    push bp
    mov bp, sp
    push di
    push bx
    push ax
    mov di, [bp + 4] ; controller pointer
    xor ax, ax
    push di
    call getCurrentAnimation
    pop bx ; current animation
    push di
    push AnimatorComponentId
    call getControllerComponent
    pop di ; component
    mov bl, [bx + AnimationSize]    ; animation size
    mov al, [di + AnimatorComponentCurrentFrameIndex] ; current frame number
    inc al                                       ; next frame
    cmp al, bl
    jne .end
    mov al, 0
.end:
    mov [di + AnimatorComponentCurrentFrameIndex], al ; set the frame
    pop ax
    pop bx
    pop di
    pop bp
    ret 2
endp moveToNextAnimationFrame

; input - controller pointer, byte animation number
; sets the current animation by number (first is 0, second is 1...)
proc setAnimation
    push bp
    mov bp, sp
    push di
    push ax
    xor ax, ax
    mov al, [bp + 4] ; animation number
    mov di, [bp + 6] ; controller pointer
    push di
    push AnimatorComponentId
    call getControllerComponent
    pop di ; animator component
    mov [byte ptr di + AnimatorComponentCurrentAnimationIndex], al  ; set current animation
    mov [byte ptr di + AnimatorComponentCurrentFrameIndex], 0       ; set current frame 0
    pop ax
    pop di
    pop bp
    ret 4
endp setAnimation

proc getFrameImage
    push bp
    mov bp, sp
    push bx
    push si

    mov bx, [bp + 6] ; animation
    mov si, [bp + 4] ; frame index

    add bx, AnimationData ; first animation
    add bx, si
    add bx, si ; current animation (every anim is a word)

    mov bx, [bx]
    mov [bp + 6], bx

    pop si
    pop bx
    pop bp
    ret 2
endp getFrameImage

; input - animated controller, current time (0-255)
; updates it's current animation
proc updateAnimation
    push bp
    mov bp, sp
    push ax
    push bx
    push di
    push cx
    mov di, [bp + 6] ; controller pointer
    mov al, [bp + 4] ; current time

    push di
    call getCurrentAnimation
    pop bx
    
    mov ch, [bx + AnimationSpeed] ; animation speed
    div ch ; ah - time % speed
    cmp ah, 0
    jne .end

    push di
    call moveToNextAnimationFrame

    push di
    push AnimatorComponentId
    push AnimatorComponentCurrentFrameIndex
    call getControllerByteValue
    pop cx
    mov ch, 0 ; cl = current frame index

    push bx
    push cx
    call getFrameImage
    pop bx

    push di
    push bx
    push RenderComponentId
    push RenderComponentImage
    call setControllerWordValue

.end:
    pop cx
    pop di
    pop bx
    pop ax
    pop bp
    ret 4
endp updateAnimation

; updates animation time
proc updateAnimationTime
    push bp
    mov bp, sp
    push bx
    mov bx, [bp + 4]    ; time pointer
    cmp [byte ptr bx], 255
    jne .continue
    mov [byte ptr bx], 0
.continue:
    inc [byte ptr bx]
    pop bx
    pop bp
    ret 2
endp updateAnimationTime

; input - printManager pointer
; prints all the controllers
proc printAllControllers
    push bp
    mov bp, sp
    push bx
    push cx
    push si
    mov si, [bp + 4] ; offset printManager
    add si, PrintManagerControllerAmount
    mov bx, si
    mov cl, [byte ptr bx] ; amount of controllers
.printBackgroundLoop:
    cmp cl, 0
    je .printBackgroundEnd
    push [bx + 1]      ; controller pointer
    call printBackground
    add bx, 2
    dec cl
    jmp .printBackgroundLoop
.printBackgroundEnd:
    mov bx, si
    mov cl, [byte ptr bx] ; amount of controllers
.saveLoop:
    cmp cl, 0
    je .saveEnd
    push [bx + 1]      ; controller pointer
    call saveController
    add bx, 2
    dec cl
    jmp .saveLoop
.saveEnd:
    mov bx, si
    mov cl, [byte ptr bx] ; amount of controllers
    mov [byte ptr bx], 0
.printImageLoop:
    cmp cl, 0
    je .end
    push [bx + 1]      ;  controller pointer
    call printController
    mov [word ptr bx + 1], 0
    add bx, 2
    dec cl
    jmp .printImageLoop
.end:
    pop si
    pop cx
    pop bx
    pop bp
    ret 2
endp printAllControllers

; input - printManager pointer, controller pointer
; uploads the controller to the manager
proc uploadController
    push bp
    mov bp, sp
    push bx
    push di
    push ax
    mov bx, [bp + 6] ; printManager pointer
    mov di, [bp + 4] ; controller pointer
    xor ax, ax
    mov al, [byte ptr bx + PrintManagerControllerAmount] ; amount of controllers
    inc [byte ptr bx + PrintManagerControllerAmount]     ; inc the amount of controllers
    shl ax, 1 ; every pointer is a word
    add bx, ax
    mov [bx + PrintManagerData], di  ; upload it
    pop ax
    pop di
    pop bx
    pop bp
    ret 4
endp uploadController

; input - animated controller
; returns the current animation
proc getCurrentAnimation
    push bp
    mov bp, sp
    push di
    push bx
    mov di, [bp + 4] ; controller pointer

    push di
    push AnimatorComponentId
    call getControllerComponent
    pop di ; component

    xor bx, bx
    mov bl, [di + AnimatorComponentCurrentAnimationIndex]  ; current animation index
    add di, AnimatorComponentData ; animation list
    shl bx, 1                     ; each animation is a word         
    add di, bx                    ; current animation
    mov di, [di]                  ; get the pointer to anim and not the pointer to the pointer
    mov [bp + 4], di

    pop bx
    pop di
    pop bp
    ret
endp getCurrentAnimation

; input - velocity controller
; update its velocity (x and y)
proc updateVelocity
    push bp
    mov bp, sp
    push bx
    push ax
    mov bx, [bp + 4] ; controller

    push bx
    push PhysicsComponentId
    call getControllerComponent
    pop bx ; component

    mov ax, bx
    add ax, PhysicsComponentAxisX
    push ax
    call updatePhysicsAxis      ; update x axis

    mov ax, bx
    add ax, PhysicsComponentAxisY
    push ax
    call updatePhysicsAxis      ; update y axis

    pop ax
    pop bx
    pop bp
    ret 2
endp updateVelocity

; input - velocity axis (start of each axis part)
; updates its velocity
proc updatePhysicsAxis
    push bp
    mov bp, sp
    push bx
    push dx
    push si
    push cx
    mov si, [bp + 4]                                ; physics axis
    mov dx, [si + PhysicsComponentAxisFriction]     ; friction
    mov bx, [si + PhysicsComponentAxisVelocity]     ; velocity
    mov cx, [si + PhysicsComponentAxisMaxVelocity]  ; max velocity
    cmp bx, 0
    jl .positiveFriction ; if we go negative
    cmp bx, 0
    jg .negativeFriction ; if we go negative

    mov bx, [si + PhysicsComponentAxisAcceleration] ; accerlation
    add [si], bx                                    ; add velocity acceleration

    jmp .end
.negativeFriction:
    sub bx, dx ; check if the gravity is too much it will become positive, if so reset it
    cmp bx, 0
    jl .resetFriction
    sub [si + PhysicsComponentAxisVelocity], dx

    mov bx, [si + PhysicsComponentAxisAcceleration] ; accerlation
    add [si], bx                                    ; add velocity acceleration

    jmp .limitPositiveVelocity
.positiveFriction:
    add bx, dx ; check if the gravity is too much it will become negative, if so reset it
    cmp bx, 0
    jg .resetFriction
    add [si + PhysicsComponentAxisVelocity], dx
    
    mov bx, [si + PhysicsComponentAxisAcceleration] ; accerlation
    add [si], bx                                    ; add velocity acceleration

    jmp .limitNegativeVelocity
.resetFriction:
    mov [word ptr si + PhysicsComponentAxisVelocity], 0
    jmp .end
.limitPositiveVelocity:
    cmp [si + PhysicsComponentAxisVelocity], cx
    jl .limitNegativeVelocity
    mov [si + PhysicsComponentAxisVelocity], cx
.limitNegativeVelocity:
    neg cx
    cmp [si + PhysicsComponentAxisVelocity], cx
    jg .end
    mov [si + PhysicsComponentAxisVelocity], cx
.end:
    pop cx
    pop si
    pop dx
    pop bx
    pop bp
    ret 2
endp updatePhysicsAxis

; input - physics controller
; adds the velocity to the controllers using the subPixelAmount
; const to enable sub-pixel movement, for example - 0.5 pixels/frame
proc applyVelocity
    push bp
    mov bp, sp
    push bx
    push ax
    push dx
    push cx
    push si

    mov bx, [bp + 4] ; controller
    mov si, SubPixelAmount

    push bx
    push PhysicsComponentId
    push PhysicsComponentVelocityY
    call getControllerWordValue
    pop ax           ; velocity y
    cwd              ; Sign-extend ax into dx:ax
    idiv si          ; div by subPixelAmount
    mov dx, ax       ; dx = new velocity Y

    sal ax, 6  ; ax = y * 64
    sal dx, 8  ; dx = y * 256
    add ax, dx
    mov cx, ax ; cx = y * 320

    push bx
    push PhysicsComponentId
    push PhysicsComponentVelocityX
    call getControllerWordValue
    pop ax           ; velocity x
    cwd              ; Sign-extend ax into dx:ax
    idiv si          ; div by subPixelAmount, ax = new velocity X

    add ax, cx ; add velocity Y
    push bx ; controller
    push ax ; addition (velocity y + x)
    push RenderComponentId
    push RenderComponentPosition
    call addControllerWordValue

    pop si
    pop cx
    pop dx
    pop ax
    pop bx
    pop bp
    ret 2
endp applyVelocity

proc updateAndApplyVelocity
    push bp
    mov bp, sp
    push bx
    mov bx, [bp + 4] ; controller
    push bx
    call updateVelocity
    push bx
    call applyVelocity
    pop bx
    pop bp
    ret 2
endp updateAndApplyVelocity

proc walk
    push bp
    mov bp, sp
    push ax
    push bx
    mov bx, [bp + 6] ; controller
    mov ax, [bp + 4] ; direction X
    cmp ax, 0
    je .skipX
    push bx
    push ax
    push PhysicsComponentId
    push PhysicsComponentVelocityX
    call setControllerWordValue
.skipX:
    pop bx
    pop ax
    pop bp
    ret 4
endp walk

proc move2d
    push bp
    mov bp, sp
    push ax
    push bx
    push dx
    mov bx, [bp + 8] ; controller
    mov ax, [bp + 6] ; direction X
    mov dx, [bp + 4] ; direction Y

    push bx
    push ax
    push PhysicsComponentId
    push PhysicsComponentVelocityX
    call setControllerWordValue

    push bx
    push dx
    push PhysicsComponentId
    push PhysicsComponentVelocityY
    call setControllerWordValue
    
    pop dx
    pop bx
    pop ax
    pop bp
    ret 6
endp move2d   

proc jump
    push bp
    mov bp, sp
    push bx
    mov bx, [bp + 4] ; controller
    cmp [byte ptr KeyStateTable + 39h], 0 ; space scan code
    je .end
    push bx
    push -1 * JumpHeight
    push PhysicsComponentId
    push PhysicsComponentVelocityY
    call setControllerWordValue
.end:
    pop bx
    pop bp
    ret 2
endp jump

proc getControllerY
    push bp
    mov bp, sp
    push ax
    push bx
    push dx
    mov bx, [bp + 4] ; first controller
    push bx
    push RenderComponentId
    push RenderComponentPosition
    call getControllerWordValue
    pop ax
    mov bx, 320
    xor dx, dx
    div bx
    mov [bp + 4], ax ; y
    pop dx
    pop bx
    pop ax
    pop bp
    ret
endp getControllerY

proc getControllerX
    push bp
    mov bp, sp
    push ax
    push bx
    push dx
    mov bx, [bp + 4] ; first controller
    push bx
    push RenderComponentId
    push RenderComponentPosition
    call getControllerWordValue
    pop ax
    mov bx, 320
    xor dx, dx
    div bx
    mov [bp + 4], dx ; x
    pop dx
    pop bx
    pop ax
    pop bp
    ret
endp getControllerX

proc getControllerYEdges
    push bp
    mov bp, sp
    push ax
    push bx
    push dx
    push di
    mov bx, [bp + 4] ; controller
    push bx
    call getControllerY
    pop ax ; top
    push bx
    push RenderComponentId
    push RenderComponentHeight
    call getControllerWordValue
    pop di
    mov dx, ax
    add dx, di       ; bottom
    mov [bp + 6], ax
    mov [bp + 4], dx
    pop di
    pop dx
    pop bx
    pop ax
    pop bp
    ret
endp getControllerYEdges

proc getControllerXEdges
    push bp
    mov bp, sp
    push ax
    push bx
    push dx
    push di
    mov bx, [bp + 4] ; controller
    push bx
    call getControllerX
    pop ax ; left
    push bx
    push RenderComponentId
    push RenderComponentWidth
    call getControllerWordValue
    pop di
    mov dx, ax
    add dx, di       ; right
    mov [bp + 6], ax
    mov [bp + 4], dx
    pop di
    pop dx
    pop bx
    pop ax
    pop bp
    ret
endp getControllerXEdges

; return the difference in the Y axis between two objects
; input - first controller, second controller
proc isYCollision
   push bp
    mov bp, sp
    push ax
    push bx
    push si
    push di
    push dx
    mov si, [bp + 6] ; first controller
    mov bx, [bp + 4] ; second controller
    push 0 ; garbage value
    push si
    call getControllerYEdges
    pop ax
    pop dx
    push 0 ; garbage value
    push bx
    call getControllerYEdges
    pop bx
    pop si
    push ax
    push dx
    push bx
    push si
    call isAxisCollision
    pop ax
    mov [bp + 6], ax
    pop dx
    pop di
    pop si
    pop bx
    pop ax
    pop bp
    ret 2
endp isYCollision

; return the difference in the X axis between two objects
; input - first controller, second controller
proc isXCollision
   push bp
    mov bp, sp
    push ax
    push bx
    push si
    push di
    push dx
    mov si, [bp + 6] ; first controller
    mov bx, [bp + 4] ; second controller
    push 0 ; garbage value
    push si
    call getControllerXEdges
    pop ax
    pop dx
    push 0 ; garbage value
    push bx
    call getControllerXEdges
    pop bx
    pop si
    push ax
    push dx
    push bx
    push si
    call isAxisCollision
    pop ax
    mov [bp + 6], ax
    pop dx
    pop di
    pop si
    pop bx
    pop ax
    pop bp
    ret 2
endp isXCollision

proc areColliding
    push bp
    mov bp, sp
    push ax
    push bx
    push si
    push cx
    push dx
    mov si, [bp + 6] ; first controller
    mov bx, [bp + 4] ; second controller
    push bx
    push si
    call isXCollision
    pop ax
    push bx
    push si
    call isYCollision
    pop dx
    cmp dx, 0
    je .setFalse
    cmp ax, 0
    je .setFalse
    mov cx, ax
    mov si, dx
    test cx, cx
    jns .xIsPositive
    neg cx
.xIsPositive:
    test si, si
    jns .yIsPositive
    neg si
.yIsPositive:
    cmp cx, si
    jb .end
    mov ax, dx
    mov dx, 320
    imul dx
    jmp .end
.setFalse:
    mov ax, 0
.end:
    mov [bp + 6], ax ; change to correctly collide
    pop dx
    pop cx
    pop si
    pop bx
    pop ax
    pop bp
    ret 2
endp areColliding

; gets the edge points of 2 objects and return the distance between them,
; and 0 if they don't collide
; input - obj1 bigger, obj2 lower, obj1 bigger, obj lower
; lower - (top/left), bigger - (bottom/right)
proc isAxisCollision
    push bp
    mov bp, sp
    push cx
    push dx
    push bx
    push ax

    mov ax, [bp + 10]  ; bottom/right1
    mov bx, [bp + 8]   ; top/left1
    mov dx, [bp + 6]   ; bottom/right2
    mov cx, [bp + 4]   ; top/left2

    ; if bottom1 is less than top2
    cmp ax, cx
    jle .setFalse
    ; if bottom2 is less than top1
    cmp dx, bx
    jle .setFalse

    cmp ax, dx ; 1 is above 2
    ja .set2
    sub ax, cx
    jmp .end
.set2:
    sub bx, dx
    mov ax, bx
    jmp .end
.setFalse:
    mov ax, 0
.end:
    mov [bp + 10], ax
    pop ax
    pop bx
    pop dx
    pop cx
    pop bp
    ret 6
endp isAxisCollision


proc getControllerComponent
    push bp
    mov bp, sp
    push ax
    push bx
    push si

    mov bx, [bp + 6] ; controller
    mov ax, [bp + 4] ; componentId

    mov si, bx
    add si, ControllerComponentData
    dec si

.componentLoop:
    inc si
    mov ah, [si] ; current component id
    cmp al, ah
    jne .componentLoop

    xor ax, ax
    mov al, [si + ComponentLocation]
    add bx, ax

    mov [bp + 6], bx
    pop si
    pop bx
    pop ax
    pop bp
    ret 2
endp getControllerComponent

proc endAndCleanUp
    push offset previousIRQMask
    call cleanUpClock

    mov ah, 0
    mov al, 2
    int 10h
    mov ax, 4C00h
    int 21h
    ret
endp endAndCleanUp

; input - the segment wrapper
; output - the original object
; note - need to execute end, to return to the normal ds
proc StartLoadFromOtherSegment
    push bp
    mov bp, sp
    push bx
    push ax
    push si
    mov bx, [bp + 4]
    mov [bp + 6], ds
    mov ax, [bx + SegmentWrapperSegment]
    mov si, [bx + SegmentWrapperOriginal]
    mov ds, ax
    mov [bp + 4], si
    pop si
    pop ax
    pop bx
    pop bp
    ret
endp StartLoadFromOtherSegment

; called 1024 times a sec
proc periodic
    push bp
    mov bp, sp
    push bx
    push cx
    push dx
    push ax
    mov bx, [bp + 4]
    inc [word ptr bx]
    cmp [word ptr bx], 1025
    jl .skipReset
    mov [word ptr bx], 0
.skipReset:
    mov ax, [bx]
    mov cx, gameLoopSpeed
    xor dx, dx
    div cx
    cmp dx, 0
    jne .skipGameLoop
    call gameLoop
.skipGameLoop:
    pop ax
    pop dx
    pop cx
    pop bx
    pop bp
    ret 2
endp periodic

; setups the keyboard handler
; input - pointer to the old segment save, another for the offset
proc setupKeyboardHandler
    push bp
    mov bp, sp
    push ax
    push es
    xor ax, ax
    mov es, ax ; set es to IVT
    cli
    ; Save the original ISR address
    mov ax, [word ptr es:9*4]
    mov [bp + 4], ax ; save old offset
    mov ax, [word ptr es:9*4+2]
    mov [bp + 6], ax ; save old segment
    ; Set the new ISR address
    mov [word ptr es:9*4], offset keyboardHandler
    mov [word ptr es:9*4+2], seg keyboardHandler
    sti
    pop es
    pop ax
    pop bp
    ret 4
endp setupKeyboardHandler

proc gameLoop


    ret
endp gameLoop

proc handleBounceCollision
   push bp
    mov bp, sp
    push bx
    push ax
    mov bx, [bp + 6] ; controller
    push bx
    push [bp + 4] ; collider
    call areColliding
    pop ax ; amount to move
    cmp ax, 0
    je .end

    push bx
    push ax
    push RenderComponentId
    push RenderComponentPosition
    call addControllerWordValue

    push bx
    push PhysicsComponentId
    call getControllerComponent
    pop bx
    mov ax, [bx + PhysicsComponentVelocityX]

    neg ax
    add [bx + PhysicsComponentVelocityX], ax
    add [bx + PhysicsComponentVelocityX], ax

.skipX:
    mov ax, [bx + PhysicsComponentVelocityY]
    neg ax
    add [bx + PhysicsComponentVelocityY], ax
    add [bx + PhysicsComponentVelocityY], ax

.end:
    pop ax
    pop bx
    pop bp
    ret 4
endp handleBounceCollision

proc handleWallCollision
    push bp
    mov bp, sp
    push bx
    push ax
    mov bx, [bp + 6] ; controller
    push bx
    push [bp + 4] ; collider
    call areColliding
    pop ax ; amount to move
    cmp ax, 0
    je .end
    cmp ax, 320
    jb .skipResetY
    cmp ax, -320
    ja .skipResetY
    push bx
    push 0
    push PhysicsComponentId
    push PhysicsComponentVelocityY
    call setControllerWordValue
    jmp .skipResetX
.skipResetY:
    push bx
    push 0
    push PhysicsComponentId
    push PhysicsComponentVelocityX
    call setControllerWordValue
.skipResetX:
    push bx
    push ax
    push RenderComponentId
    push RenderComponentPosition
    call addControllerWordValue
.end:
    pop ax
    pop bx
    pop bp
    ret 4
endp handleWallCollision

start:
    mov ax, @data
    mov ds, ax
    mov ax, 13h
    int 10h
    mov ax, 0A000h
    mov es, ax

    push offset palette
    call setPalette

    call clearScreen

; *******************INIT**************
; Note - here order is important, because they save their background here
   

; **************************************

    push offset oldKeyboardHandlerSegment
    push offset oldKeyboardHandlerOffset
    call setupKeyboardHandler

    push offset previousIRQMask
    call setUpClock

keepAliveLoop:
    jmp keepAliveLoop
exit:
    call endAndCleanUp
END start
